buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/plugins-release' }
    }
}

plugins {
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenLocal()
    mavenCentral()
}

ext['elasticsearch.version'] = '7.8.1'

dependencies {
    implementation (
            "org.openmbee.mms:authenticator:${mmsVersion}",
            "org.openmbee.mms:localuser:${mmsVersion}",
            "org.openmbee.mms:ldap:${mmsVersion}",
            "org.openmbee.mms:crud:${mmsVersion}",
            "org.openmbee.mms:cameo:${mmsVersion}",
            "org.openmbee.mms:elastic:${mmsVersion}",
            "org.openmbee.mms:jupyter:${mmsVersion}",
            "org.openmbee.mms:permissions:${mmsVersion}",
            "org.openmbee.mms:webhooks:${mmsVersion}",
            "org.openmbee.mms:search:${mmsVersion}",
            "org.openmbee.mms:storage:${mmsVersion}",
            "org.openmbee.mms:groups:${mmsVersion}",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.postgresql:postgresql:42.2.27",
            "org.zalando:logbook-spring-boot-starter:2.2.1",
            "ch.qos.logback:logback-classic:1.2.11",
            'org.springdoc:springdoc-openapi-ui:1.6.9',
            "org.springdoc:springdoc-openapi-webmvc-core:1.6.9",
            "org.springdoc:springdoc-openapi-security:1.6.9",
            //resolve https://nvd.nist.gov/vuln/detail/CVE-2022-1471
            "org.yaml:snakeyaml:2.0",
            "org.apache.lucene:lucene-core:8.10.0",
            "com.fasterxml.jackson.core:jackson-core:2.15.0",
    )
    testImplementation(
            'org.springframework.boot:spring-boot-starter-test'
    )
    swaggerCodegen 'org.openapitools:openapi-generator-cli:5.0.1'
}

swaggerSources {
    python {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'python'
            configFile = file('openapi/python-config.json')
        }
    }
    java {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'java'
            configFile = file('openapi/java-config.json')
        }
    }
    doc {
        inputFile = file('openapi/api-docs.yaml')
        code {
            language = 'html2'
            configFile = file('openapi/doc-config.json')
        }
    }
}

springBoot {
    buildInfo()
}

bootJar {
    layered {
        enabled = true
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

//https://stackoverflow.com/questions/61197984/bootjar-mavenjar-artifact-wasnt-produced-by-this-build
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}
